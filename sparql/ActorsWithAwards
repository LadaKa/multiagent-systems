# Actors that received the Oscar award (Academy Award) 
# sorted by the total number of (any) awards they received 
# in decreasing order 
# together with the list of all their awards.

SELECT 
  ?actor 
  ?actorLabel 
  ?awardsNumber 
  ?awards
WHERE 
{
  {
    SELECT ?actor (COUNT(DISTINCT ?award) AS ?awardsNumber) (GROUP_CONCAT(DISTINCT(?award); separator=", ") as ?awards)
    WHERE {
       ?actor wdt:P106 wd:Q33999 . # has occupation actor
       ?actor wdt:P166 wd:Q103916. # received Academy Award for Best Actor
       ?actor wdt:P166 ?award.     # received any award
    }  
    GROUP BY ?actor
  }         
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
}

ORDER BY DESC (?awardsNumber)
