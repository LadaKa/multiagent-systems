# https://query.wikidata.org

# Actors that received the Oscar award (Academy Award) 
# sorted by the total number of (any) awards they received 
# in decreasing order 
# together with the list of all their awards.

SELECT 
  ?actor 
  ?actorLabel 
  ?awardsNumber  # total number of (any) awards 
  ?awards        # list of all received awards
WHERE 
{
  {
    SELECT 
      ?actor 
      (COUNT(DISTINCT ?award) AS ?awardsNumber)   
      (GROUP_CONCAT(DISTINCT(?label); separator=", ") as ?awards)  
      
    WHERE {
      ?actor wdt:P106 wd:Q33999 . # has occupation actor
      ?actor wdt:P166 wd:Q103916. # received Academy Award for Best Actor
      ?actor wdt:P166 ?award.     # received any award
      ?award rdfs:label ?label.   # label of the received award in English
      FILTER (langMatches( lang(?label), "EN" ))
    }  
    GROUP BY ?actor
  }         
  SERVICE wikibase:label { bd:serviceParam wikibase:language "en" . }
}

ORDER BY DESC (?awardsNumber)
